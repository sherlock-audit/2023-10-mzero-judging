Droll Iron Ferret

medium

# Possible overflow when minting M

## Summary

Use of unchecked math allows to mint amounts which would overflow balances such that attackers can arbitrarily set the totalSupply.

## Vulnerability Detail

In `_revertIfUndercollateralized`, we use **unchecked** math to check whether the users resulting debt will exceed the maximum given the amount of collateral they have:

```solidity
function _revertIfUndercollateralized(address minter_, uint240 additionalOwedM_) internal view {
    uint256 maxAllowedActiveOwedM_ = maxAllowedActiveOwedMOf(minter_);

    // If the minter's max allowed active owed M is greater than the max uint240, then it's definitely greater than
    // the max possible active owed M for the minter, which is capped at the max uint240.
    if (maxAllowedActiveOwedM_ >= type(uint240).max) return;

    unchecked {
        uint256 finalActiveOwedM_ = uint256(activeOwedMOf(minter_)) + additionalOwedM_;

        if (finalActiveOwedM_ > maxAllowedActiveOwedM_) {
            revert Undercollateralized(finalActiveOwedM_, maxAllowedActiveOwedM_);
        }
    }
}
```

Since this math is unchecked, we can overflow by providing a large enough amount that the overflowed amount is less than the maximum. Since this is the only relevant check, this will allow proposeMint to succeed:

```solidity
function proposeMint(
    uint256 amount_,
    address destination_
) external onlyActiveMinter(msg.sender) onlyUnfrozenMinter returns (uint48 mintId_) {
    if (amount_ == 0) revert ZeroMintAmount();
    if (destination_ == address(0)) revert ZeroMintDestination();

    uint240 safeAmount_ = UIntMath.safe240(amount_);

    _revertIfUndercollateralized(msg.sender, safeAmount_); // Ensure minter remains sufficiently collateralized.

    unchecked {
        mintId_ = ++_mintNonce;
    }

    _mintProposals[msg.sender] = MintProposal(mintId_, uint40(block.timestamp), destination_, safeAmount_);

    emit MintProposed(mintId_, msg.sender, safeAmount_, destination_);
}
```

After the mintDelay, the user can then call mintM where the `_revertIfUndercollateralized` check will again succeed. Assuming that the amount to mint falls in line with the `newPrincipalOfTotalActiveOwedM_` also overflowing such that the resulting value is `< type(uint112).max`, and since the `MToken._mint` also uses similar unchecked logic, execution will fully succeed, minting the tokens.

## Impact

The result of this attack is the ability to arbitrarily set principalOfTotalActiveOwedM, which affects the earner rate, and which if close to or at the type(uint112).max, will prevent additional tokens from being minted.

Since the attack requires the passing of the mintDelay without any validators cancelling, yet has a high impact, we set the severity to medium.

## Code Snippet

https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/MinterGateway.sol#L1018-L1032

## Tool used

Manual Review

## Recommendation

Use checked math in `_revertIfUndercollateralized`. Additionally, consider using checked math in similarly important logic such as `MToken._mint`.
Young Oily Lark

medium

# Stable Earner Rate Model brick core functions of Minter Gateway due to 0 initial rate

## Summary
`MinterGateway` has a starting rate of 0 and the `StableEarnerRateModel` relies on the starting rate for its rate calculation.  It can break the rate calculation leading it to constantly revert

## Vulnerability Detail
The Minter Gateway updates its latest rate via updateIndex() and it starts with a rate of 0. 

ref: https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/abstract/ContinuousIndexing.sol#L28-L49
```solidity
constructor() {
    latestIndex = ContinuousIndexingMath.EXP_SCALED_ONE;
    latestUpdateTimestamp = uint40(block.timestamp);
}
// ... snip ...

function updateIndex() public virtual returns (uint128 currentIndex_) {
    // NOTE: `_rate()` can depend indirectly on `latestIndex` and `latestUpdateTimestamp`, if the RateModel
    //       depends on earning balances/supply, which depends on `currentIndex()`, so only update them after this.
    // static value set by TTG
    uint32 rate_ = _rate();

    if (latestUpdateTimestamp == block.timestamp && _latestRate == rate_) return latestIndex;

    // NOTE: `currentIndex()` depends on `_latestRate`, so only update it after this.
    // e^ (interest over time)
    latestIndex = currentIndex_ = currentIndex();
    _latestRate = rate_;
    latestUpdateTimestamp = uint40(block.timestamp);

    emit IndexUpdated(currentIndex_, rate_);
}
```

When the Minter Gateway uses the Stable Earner Rate Model as its rate model, the gateway relies on it for rate calculation.

ref: https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/rateModels/StableEarnerRateModel.sol#L67-L75
```solidity
function rate() external view returns (uint256) {
    uint256 safeEarnerRate_ = getSafeEarnerRate(
        IMinterGateway(minterGateway).totalActiveOwedM(),
        IMToken(mToken).totalEarningSupply(),
        IMinterGateway(minterGateway).minterRate()
    );

    return UIntMath.min256(maxRate(), (RATE_MULTIPLIER * safeEarnerRate_) / ONE);
}
```

Note that Minter Gateway's latest rate starts at 0. That means that minterRate() will be 0 the first time the gateway updates its index. This leads to the Stable Earner rate calculation to either revert or return 0.

It returns a rate of 0 when total owed M by active minters is less than or equal to the total supply of M tokens that are earning. 

ref: https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/rateModels/StableEarnerRateModel.sol#L115-L119
```solidity
function getSafeEarnerRate(
    uint240 totalActiveOwedM_,
    uint240 totalEarningSupply_,
    uint32 minterRate_
) public pure returns (uint32) {
    // ... snip ...
    if (totalActiveOwedM_ <= totalEarningSupply_) {
        return uint32((uint256(totalActiveOwedM_) * minterRate_) / totalEarningSupply_);
    }
    // ... snip ...
}
```

The rate calculation always reverts when the total owed M by active minters is greater than the total earning supply of M tokens. 

ref: https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/rateModels/StableEarnerRateModel.sol#L121-L135
```solidity
function getSafeEarnerRate(
    uint240 totalActiveOwedM_,
    uint240 totalEarningSupply_,
    uint32 minterRate_
) public pure returns (uint32) {
    // ... snip ...
    // @audit deltaMinterIndex_ is always 1e12 when minterRate is 0
    uint48 deltaMinterIndex_ = ContinuousIndexingMath.getContinuousIndex(
        ContinuousIndexingMath.convertFromBasisPoints(minterRate_),
        RATE_CONFIDENCE_INTERVAL
    );

    // @audit for any `totalEarningSupply_` greater than 1, `lnArg_` will always be a value less than 1e12
    int256 lnArg_ = int256(
        _EXP_SCALED_ONE +
            ((uint256(totalActiveOwedM_) * (deltaMinterIndex_ - _EXP_SCALED_ONE)) / totalEarningSupply_)
    );
    // @audit `lnResult` will always be a negative value when `lnArg_` is less than 1e12, which is usually the case
    int256 lnResult_ = wadLn(lnArg_ * _WAD_TO_EXP_SCALER) / _WAD_TO_EXP_SCALER;
    // @audit the computation below always reverts. likely because of converting the negative value to uint256
    uint256 expRate_ = (uint256(lnResult_) * ContinuousIndexingMath.SECONDS_PER_YEAR) / RATE_CONFIDENCE_INTERVAL;

    // ... snip ...
}
```

## Impact
When `totalActiveOwedM_ >= totalEarningSupply_`, which is the default case, the core functionalities of Minter Gateway will constantly revert for the common case. Below are all the core functions that are affected:
1. `deactivateMinter()`
2. `burnM()`
3. `mintM()`
4. `updateCollateral()`
 
The only case when `updateIndex()` will work properly is if there is only 1 `totalEarningSupply` of M tokens, which is highly unlikely. The Minter Gateway will effectively be unavailable when it uses the `StableEarnerRateModel` as its initial rate model.

## Code Snippet
- https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/abstract/ContinuousIndexing.sol#L28-L49
- https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/rateModels/StableEarnerRateModel.sol#L67-L75
- https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/rateModels/StableEarnerRateModel.sol#L115-L119
- https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/rateModels/StableEarnerRateModel.sol#L121-L135
- https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/MinterGateway.sol#L443
- https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/MinterGateway.sol#L210

## Tool used
Manual Review, Remix

## Recommendation
Consider modifying the rate calculation in Stable Earner Rate Model to use a reasonable default value for minter rate if it is 0.

## POC
Linked below is a contract that can be deployed in Remix and tested to more clearly show the issue.
https://gist.github.com/gjaldon/a686e17596f6765c0bee5777d85ecf3b
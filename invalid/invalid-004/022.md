Fantastic Porcelain Mantaray

medium

# Voting bias due to incentive of significant gas saving

## Summary
To gain ````PowerToken```` inflation reward of some epoch, users must cast for all proposals in that epoch, as the protocol is intended to be deployed on Ethereum, users' cumulative gas cost would be one of important costs for participating in the project. The issue here is that if a second and subsequent users cast ````YES/NO```` same with the first voter, they can save significant gas. It's a strong incentive which would lead to voting bias.

## Vulnerability Detail
Per EVM current implementation, the gas cost for state variable is as follows:
```solidity
0 -> nonZero costs 22_100 gas
nonZero -> other nonZero costs 5_000 gas
```
Now, let's look at L482 and L484, as a second or subsequent voter, if we cast same with the first voter, it only costs ````5_000```` to update ````noWeight/yesWeight````, otherwise costs ````22_100````.
```solidity
File: src\abstract\BatchGovernor.sol
468:     function _castVote(
...
474:     ) internal virtual {
...
...
481:             if (VoteType(support_) == VoteType.No) {
482:                 _proposals[proposalId_].noWeight += weight_;
483:             } else {
484:                 _proposals[proposalId_].yesWeight += weight_;
485:             }
...
489:     }
```

The following PoC shows the case that a batch cast for 10 proposals could save 171K gas, which is a ````~30%```` gas saving and worth about $30 at the writing time.
```solidity
// SPDX-License-Identifier: MIT

pragma solidity 0.8.23;

import { IBatchGovernor } from "../src/abstract/interfaces/IBatchGovernor.sol";
import { IStandardGovernor } from "../src/interfaces/IStandardGovernor.sol";
import { IGovernor } from "../src/abstract/interfaces/IGovernor.sol";

import { StandardGovernorHarness } from "./utils/StandardGovernorHarness.sol";
import { MockERC20, MockPowerToken, MockRegistrar, MockZeroToken } from "./utils/Mocks.sol";
import { TestUtils } from "./utils/TestUtils.sol";
import { console2 } from "forge-std/console2.sol";

contract IncentiveOfVotingBiasTest is TestUtils {
    address alice = makeAddr("alice");
    address bob = makeAddr("bob");
    address emergencyGovernor = makeAddr("emergencyGovernor");
    address vault = makeAddr("vault");
    address zeroGovernor = makeAddr("zeroGovernor");

    uint256 maxTotalZeroRewardPerActiveEpoch = 1_000;
    uint256 proposalFee = 5;

    StandardGovernorHarness internal standardGovernor;

    MockERC20 cashToken;
    MockPowerToken powerToken;
    MockRegistrar registrar;
    MockZeroToken zeroToken;
    uint256[] proposalIds;
    uint8[] firstSupports;

    function setUp() public {
        // reference: StandardGovernor.t.sol
        cashToken = new MockERC20();
        powerToken = new MockPowerToken();
        powerToken.setVotePower(1);
        powerToken.setPastTotalSupply(10);
        zeroToken = new MockZeroToken();
        registrar = new MockRegistrar();

        standardGovernor = new StandardGovernorHarness(
            address(powerToken),
            emergencyGovernor,
            zeroGovernor,
            address(cashToken),
            address(registrar),
            vault,
            address(zeroToken),
            proposalFee,
            maxTotalZeroRewardPerActiveEpoch
        );

        // set up proposals
        for (uint256 i; i < 10; ++i) {
            proposalIds.push(_makeProposal(i));
        }

        // cast first votes
        _warpToNextVoteEpoch();
        for (uint256 i; i < 10; ++i) {
            firstSupports.push(uint8(i % 2));
        }
        standardGovernor.castVotes(proposalIds, firstSupports);
    }

    function testGasSavingCase() public {
        uint256[] memory proposals = proposalIds;
        address governor = address(standardGovernor);

        // alice keeps same "YES/NO" with first voter
        uint8[] memory aliceSupports = new uint8[](10);
        for (uint256 i; i < 10; ++i) {
            aliceSupports[i] = firstSupports[i];
        }
        bytes memory aliceCalldata = abi.encodeWithSelector(
            standardGovernor.castVotes.selector,
            proposals,
            aliceSupports
        );
        bool success;
        vm.prank(alice);
        uint256 gasBebore = gasleft();
        assembly {
            // low level call is more precise
            success := call(gasBebore, governor, 0, add(aliceCalldata, 0x20), mload(aliceCalldata), 0, 0)
        }
        uint256 gasAfter = gasleft();
        uint256 aliceGasUsed = gasBebore - gasAfter;
        assertTrue(success);

        console2.log(aliceGasUsed);
    }

    function testGasWasteCase() public {
        uint256[] memory proposals = proposalIds;
        address governor = address(standardGovernor);

        // alice uses different "YES/NO" with first voter
        uint8[] memory aliceSupports = new uint8[](10);
        for (uint256 i; i < 10; ++i) {
            aliceSupports[i] = firstSupports[i] ^ 1;
        }
        bytes memory aliceCalldata = abi.encodeWithSelector(
            standardGovernor.castVotes.selector,
            proposals,
            aliceSupports
        );
        bool success;
        vm.prank(alice);
        uint256 gasBebore = gasleft();
        assembly {
            // low level call is more precise
            success := call(gasBebore, governor, 0, add(aliceCalldata, 0x20), mload(aliceCalldata), 0, 0)
        }
        uint256 gasAfter = gasleft();
        uint256 aliceGasUsed = gasBebore - gasAfter;
        assertTrue(success);
        console2.log(aliceGasUsed);
    }

    function _makeProposal(uint256 fee) internal returns (uint256) {
        address[] memory targets = new address[](1);
        targets[0] = address(standardGovernor);

        bytes[] memory callDatas = new bytes[](1);
        callDatas[0] = abi.encodeWithSelector(standardGovernor.setProposalFee.selector, fee);
        return standardGovernor.propose(targets, new uint256[](1), callDatas, "");
    }
}

```
The test log:
```solidity
2023-10-mzero-ydspa\ttg> forge test --mc IncentiveOfVotingBiasTest -vv
[⠔] Compiling...No files changed, compilation skipped
[⠒] Compiling...

Running 2 tests for test/IncentiveOfVotingBias.t.sol:IncentiveOfVotingBiasTest
[PASS] testGasSavingCase() (gas: 461091)
Logs:
  410521

[PASS] testGasWasteCase() (gas: 632173)
Logs:
  581521

Test result: ok. 2 passed; 0 failed; 0 skipped; finished in 14.52ms

Ran 1 test suites: 2 tests passed, 0 failed, 0 skipped (2 total tests)  
```

## Impact
Voting fairness is affected

## Code Snippet
https://github.com/sherlock-audit/2023-10-mzero/blob/main/ttg/src/StandardGovernor.sol#L144

## Tool used

Manual Review

## Recommendation
Set ````proposal.noWeight/yesWeight```` both to some nonzero initial value such as 1
```diff
diff --git a/ttg/src/abstract/ThresholdGovernor.sol b/ttg/src/abstract/ThresholdGovernor.sol
index 8dd3ab8..934cd4e 100644
--- a/ttg/src/abstract/ThresholdGovernor.sol
+++ b/ttg/src/abstract/ThresholdGovernor.sol
@@ -82,8 +82,8 @@ abstract contract ThresholdGovernor is IThresholdGovernor, BatchGovernor {
         voteStart_ = proposal_.voteStart;
         voteEnd_ = _getVoteEnd(proposal_.voteStart);
         state_ = state(proposalId_);
-        noVotes_ = proposal_.noWeight;
-        yesVotes_ = proposal_.yesWeight;
+        noVotes_ = proposal_.noWeight - 1;
+        yesVotes_ = proposal_.yesWeight - 1;
         proposer_ = proposal_.proposer;
         thresholdRatio_ = proposal_.thresholdRatio;
     }
@@ -146,8 +146,8 @@ abstract contract ThresholdGovernor is IThresholdGovernor, BatchGovernor {
             proposer: msg.sender,
             thresholdRatio: thresholdRatio,
             quorumRatio: 0,
-            noWeight: 0,
-            yesWeight: 0
+            noWeight: 1,
+            yesWeight: 1
         });
     }
```
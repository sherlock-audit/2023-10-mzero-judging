Ambitious Lace Eagle

medium

# `MToken.sol#_addNonEarningAmount` function may cause overflow error.

## Summary
`MToken.sol#_addNonEarningAmount` function may cause overflow error when it is called from `MToken.sol#_transfer` function.

## Vulnerability Detail
`MToken.sol#_addNonEarningAmount` function is the following.
```solidity
    function _addNonEarningAmount(address account_, uint240 amount_) internal {
        // NOTE: Safe to use unchecked here since overflow of the total supply is checked in `_mint`.
        unchecked {
            _balances[account_].rawBalance += amount_;
199:        totalNonEarningSupply += amount_;
        }
    }
```
As described in comment, the above function will not cause overflow error only when it is called from the following `MToken.sol#_mint` function.
```solidity
    function _mint(address recipient_, uint256 amount_) internal {
        _revertIfInsufficientAmount(amount_);
        _revertIfInvalidRecipient(recipient_);

        emit Transfer(address(0), recipient_, amount_);

        uint240 safeAmount_ = UIntMath.safe240(amount_);

        unchecked {
            // As an edge case precaution, prevent a mint that, if all tokens (earning and non-earning) were converted
            // to a principal earning amount, would overflow the `uint112 principalOfTotalEarningSupply`.
            if (
239:            uint256(totalNonEarningSupply) + safeAmount_ > type(uint240).max ||
                // NOTE: Round the principal up for worst case.
241:            uint256(principalOfTotalEarningSupply) +
                    _getPrincipalAmountRoundedUp(totalNonEarningSupply + safeAmount_) >=
                type(uint112).max
            ) {
                revert OverflowsPrincipalOfTotalSupply();
            }
        }

        if (_balances[recipient_].isEarning) {
            // NOTE: When minting a present amount, round the principal down in favor of the protocol.
            _addEarningAmount(recipient_, _getPrincipalAmountRoundedDown(safeAmount_));
            updateIndex();
        } else {
254:        _addNonEarningAmount(recipient_, safeAmount_);
        }
    }
```
As can be seen, `L239` prevent `_addNonEarningAmount` function from overflow error.
But, `_addNonEarningAmount` function may cause overflow error when it is called from the following `MToken.sol#_transfer` function.
```solidity
    function _transfer(address sender_, address recipient_, uint256 amount_) internal override {
        _revertIfInvalidRecipient(recipient_);

        emit Transfer(sender_, recipient_, amount_);

        uint240 safeAmount_ = UIntMath.safe240(amount_);

        bool senderIsEarning_ = _balances[sender_].isEarning; // Only using the sender's earning status more than once.

        // If this is an in-kind transfer, then...
        if (senderIsEarning_ == _balances[recipient_].isEarning) {
            // NOTE: When subtracting a present amount from an earner, round the principal up in favor of the protocol.
            return
                _transferAmountInKind( // perform an in-kind transfer with...
                    sender_,
                    recipient_,
                    senderIsEarning_ ? _getPrincipalAmountRoundedUp(safeAmount_) : safeAmount_ // the appropriate amount
                );
        }

        // If this is not an in-kind transfer, then...
        if (senderIsEarning_) {
            // either the sender is earning and the recipient is not, or...
            // NOTE: When subtracting a present amount from an earner, round the principal up in favor of the protocol.
            _subtractEarningAmount(sender_, _getPrincipalAmountRoundedUp(safeAmount_));
385:        _addNonEarningAmount(recipient_, safeAmount_);
        } else {
            // the sender is not earning and the recipient is.
            // NOTE: When adding a present amount to an earner, round the principal down in favor of the protocol.
            _subtractNonEarningAmount(sender_, safeAmount_);
            _addEarningAmount(recipient_, _getPrincipalAmountRoundedDown(safeAmount_));
        }

        updateIndex();
    }
```
On the other hand, Since `L385` is the case of transfering from earning account to non-earning account, `safeAmount < _getPresentAmount(principalOfTotalEarningSupply)` holds true. 
Also, inequality of `L241` ensures that `principalOfTotalEarningSupply + _getPrincipalAmountRoundedUp(totalNonEarningSupply) < type(uint112).max` after `_mint` function executed.
Using above two inequalities, we obtain the following formula in `L199`.
```solidity
    totalNonEarningSupply + amount_ < totalNonEarningSupply + _getPresentAmount(principalOfTotalEarningSupply)
                                    = _getPresentAmount(principalOfTotalEarningSupply + _getPrincipalAmountRoundedUp(totalNonEarningSupply))
                                    < _getPresentAmount(type(uint112).max)
                                    = type(uint112).max * currentIndex()
``` 
Therefore, when `currentIndex()` is sufficiently small, no overflow occurs even if `_addNonEarningAmount` function is called from `_transfer` function.
That is, in order to occur overflow in `_addNonEarningAmount` function, the value of `currentIndex()` should be greater than `2^240 / 2^112 = 2^128`.

`ContinuousIndexingMath.t.sol#L192` is the following.
```solidity
        assertEq(continuousIndexingMath.getContinuousIndex(_EXP_SCALED_ONE, 365 days), 2718281718281); // 2.71828183
```
Since the initial value of `currentIndex()` is `1e12` and it increases by `2.71828183` times per year, we can obtain `1e12 * 2.71828183 ^ 61 â‰ˆ 2 ^ 128`.
Thus, after about 60 years from deploy, overflow may occur when `_addNonEarningAmount` function is called from `_transfer` function.

## Impact
After about 60 years from deploy, `_addNonEarningAmount` function may cause overflow error.
It causes serious problems to the protocol.  

## Code Snippet
https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/MToken.sol#L385

## Tool used

Manual Review

## Recommendation
Modify `MToken.sol#_transfer` function as follows.
```solidity
    function _transfer(address sender_, address recipient_, uint256 amount_) internal override {
        ......
        // If this is not an in-kind transfer, then...
        if (senderIsEarning_) {
++          if (uint256(totalNonEarningSupply) + safeAmount_ > type(uint240).max) {
++              revert OverflowsPrincipalOfTotalSupply();
++          }
            // either the sender is earning and the recipient is not, or...
            // NOTE: When subtracting a present amount from an earner, round the principal up in favor of the protocol.
            _subtractEarningAmount(sender_, _getPrincipalAmountRoundedUp(safeAmount_));
            _addNonEarningAmount(recipient_, safeAmount_);
        } else {
            // the sender is not earning and the recipient is.
            // NOTE: When adding a present amount to an earner, round the principal down in favor of the protocol.
            _subtractNonEarningAmount(sender_, safeAmount_);
            _addEarningAmount(recipient_, _getPrincipalAmountRoundedDown(safeAmount_));
        }
        ......
    }
```
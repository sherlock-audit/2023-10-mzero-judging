Young Umber Okapi

high

# Lack of slippage control in buy order

## Summary

The lack of slippage control in the buy order could result in users paying a higher price than they are willing to.

## Vulnerability Detail

When users submit the `PowerToken.buy` transaction to purchase Power Token from the auction, they can define the `expiryEpoch_`. The `expiryEpoch_`  ensures that the buy transaction will only be executable till `expiryEpoch_` epoch. Beyond `expiryEpoch_` epoch, the transaction will revert as per Line 132 below.

Typically, users will define the buy transaction as valid until the end of the current transfer epoch. However, in some cases, some users would define a `expiryEpoch_` that allows their buy order to span across multiple transfer epochs. 

Assume that Bob defines a buy order with an `expiryEpoch_` that allows his order to span across three (3) transfer epochs.

Assume that the price of the auction starts at 100 for all three transfer epochs for simplicity's sake. 

Around halfway through the auction of the first transfer epoch, the price reached 50, and Bob decided to submit a buy order. Bob is only willing to pay a price of up to 50 (Maximum price of 50) for the power tokens in the auction. Suppose a buy order only spans a single transfer epoch. In that case, there is no need for any slippage control such as `maxPrice` because the later the TX is executed, the cheaper the power tokens will be, which actually benefits Bob.

However, it is different if a buy order spans across multiple transfer epochs. Assume Bob's buy order is not executed in the first transfer epoch due to many users attempting to purchase during the first auction. At the start of the second transfer epoch, when the price is 90, Bob's buy order might be executed, and he has to pay a price of 90 for the power tokens. This price exceeds the maximum price he is willing to pay (50).

https://github.com/sherlock-audit/2023-10-mzero/blob/main/ttg/src/PowerToken.sol#L124

```solidity
File: PowerToken.sol
124:     function buy(
125:         uint256 minAmount_,
126:         uint256 maxAmount_,
127:         address destination_,
128:         uint16 expiryEpoch_
129:     ) external returns (uint240 amount_, uint256 cost_) {
130:         // NOTE: Buy order has an epoch-based expiration logic to avoid user's unfair purchases in subsequent auctions.
131:         //       Order should be typically valid till the end of current transfer epoch.
132:         if (_clock() > expiryEpoch_) revert ExpiredBuyOrder();
133:         if (minAmount_ == 0 || maxAmount_ == 0) revert ZeroPurchaseAmount();
134: 
135:         uint240 amountToAuction_ = amountToAuction();
136:         uint240 safeMinAmount_ = UIntMath.safe240(minAmount_);
137:         uint240 safeMaxAmount_ = UIntMath.safe240(maxAmount_);
138: 
139:         amount_ = amountToAuction_ > safeMaxAmount_ ? safeMaxAmount_ : amountToAuction_;
140: 
141:         if (amount_ < safeMinAmount_) revert InsufficientAuctionSupply(amountToAuction_, safeMinAmount_);
142: 
143:         emit Buy(msg.sender, amount_, cost_ = getCost(amount_));
144: 
145:         _mint(destination_, amount_);
146: 
147:         // NOTE: Not calling `distribute` on vault since anyone can do it, anytime, and this contract should not need to
148:         //       know how the vault works
149:         if (!ERC20Helper.transferFrom(cashToken(), msg.sender, vault, cost_)) revert TransferFromFailed();
150:     }
```

## Impact

Lack of slippage control, resulting in users having to pay a higher price than they are willing to.

## Code Snippet

https://github.com/sherlock-audit/2023-10-mzero/blob/main/ttg/src/PowerToken.sol#L124

## Tool used

Manual Review

## Recommendation

Consider implementing the following slippage control to give users the option to halt the transaction if it exceeds the price they are willing to pay.

```diff
function buy(
    uint256 minAmount_,
    uint256 maxAmount_,
    address destination_,
+	uint256 maxPrice_,    
    uint16 expiryEpoch_
) external returns (uint240 amount_, uint256 cost_) {
    emit Buy(msg.sender, amount_, cost_ = getCost(amount_));
+   if (cost_ > maxPrice_) revert CostExceeded();
```
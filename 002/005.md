Ambitious Lace Eagle

medium

# Penalties are applied wrong.

## Summary
If minter doesn't update one's collateral in time, penalties are applied to the minter.
The penalty rate increase linearly by `penaltyRate` at just `updateCollateralInterval`. That is, after `updateCollateralInterval*2`, the penalty rate should be `penaltyRate * 2`.
However, penalty rate increases exponentially when applied twice at `updateCollateralInterval` intervals.
That is, penalty rate will be `(1 + penaltyRate) ^ 2 - 1`, which is larger than `penaltyRate * 2`.

## Vulnerability Detail
`MinterGateway.sol#_imposePenaltyIfMissedCollateralUpdates` function is the following.
```solidity
    function _imposePenaltyIfMissedCollateralUpdates(address minter_) internal {
        uint32 updateCollateralInterval_ = updateCollateralInterval();

        MinterState storage minterState_ = _minterStates[minter_];

        (uint40 missedIntervals_, uint40 missedUntil_) = _getMissedCollateralUpdateParameters(
            minterState_.updateTimestamp,
            minterState_.penalizedUntilTimestamp,
            updateCollateralInterval_
        );

        if (missedIntervals_ == 0) return;

        // Save until when the minter has been penalized for missed intervals to prevent double penalizing them.
        _minterStates[minter_].penalizedUntilTimestamp = missedUntil_;

        uint112 principalOfActiveOwedM_ = principalOfActiveOwedMOf(minter_);

        if (principalOfActiveOwedM_ == 0) return;

759:    _imposePenalty(minter_, uint152(principalOfActiveOwedM_) * missedIntervals_);
    }
```
`MinterGateway.sol#_imposePenalty` function in `L759` is the following.
```solidity
    function _imposePenalty(address minter_, uint152 principalOfPenaltyBase_) internal {
        if (principalOfPenaltyBase_ == 0) return;

        uint32 penaltyRate_ = penaltyRate();

        if (penaltyRate_ == 0) return;

        unchecked {
715:        uint256 penaltyPrincipal_ = (uint256(principalOfPenaltyBase_) * penaltyRate_) / ONE;

            // As an edge case precaution, cap the penalty principal such that the resulting principal of total active
            // owed M plus the penalty principal is not greater than the max uint112.
            uint256 newPrincipalOfTotalActiveOwedM_ = principalOfTotalActiveOwedM + penaltyPrincipal_;

            if (newPrincipalOfTotalActiveOwedM_ > type(uint112).max) {
                penaltyPrincipal_ = type(uint112).max - principalOfTotalActiveOwedM;
                newPrincipalOfTotalActiveOwedM_ = type(uint112).max;
            }

            // Calculate and add penalty principal to total minter's principal of active owed M
            principalOfTotalActiveOwedM = uint112(newPrincipalOfTotalActiveOwedM_);

            _rawOwedM[minter_] += uint112(penaltyPrincipal_); // Treat rawOwedM as principal since minter is active.

            emit PenaltyImposed(minter_, uint112(penaltyPrincipal_), _getPresentAmount(uint112(penaltyPrincipal_)));
        }
    }
```
For instance, assume that `penaltyRate() == 10%`, `updateCollateralInterval() == 12 hours` and `minter1` calls `updateCollateral` function at 26 hours from the last update time.
Since `26 hours > 12 hours * 2`, the penalty rate should be `2 * 10% = 20%` by `L759` and `L715`.

On the other hand, assume that a `holder1` calls the following `burnM` function for `minter1` with extremely small `maxPriceAmount_` and `maxAmount_` at 13 hours from the last update time.
```solidity
    function burnM(
        address minter_,
        uint256 maxPrincipalAmount_,
        uint256 maxAmount_
    ) public returns (uint112 principalAmount_, uint240 amount_) {
        if (maxPrincipalAmount_ == 0 || maxAmount_ == 0) revert ZeroBurnAmount();

        bool isActive_ = _minterStates[minter_].isActive;

        if (isActive_) {
            // NOTE: Penalize only for missed collateral updates, not for undercollateralization.
            // Undercollateralization within one update interval is forgiven.
343:        _imposePenaltyIfMissedCollateralUpdates(minter_);

            (principalAmount_, amount_) = _repayForActiveMinter(
                minter_,
                UIntMath.safe112(maxPrincipalAmount_),
                UIntMath.safe240(maxAmount_)
            );

            emit BurnExecuted(minter_, principalAmount_, amount_, msg.sender);
        } else {
            amount_ = _repayForInactiveMinter(minter_, UIntMath.safe240(maxAmount_));

            emit BurnExecuted(minter_, amount_, msg.sender);
        }

        IMToken(mToken).burn(msg.sender, amount_); // Burn actual M tokens

        // NOTE: Above functionality already has access to `currentIndex()`, and since the completion of the burn
        //       can result in a new rate, we should update the index here to lock in that rate.
        updateIndex();
    }
```
Then, the penalties of `10%` are applied to `minter1` in `L343`.
Thus, if `minter1` calls `updateCollateral` at 26 hours from the last update time, the penalty rate will be `110% * 110% - 100% = 121% - 100% = 21% > 20%`.

That is, the penalty rate will be larger than `minter1`'s expectation.

## Impact
If minter doesn't call `updateCollateral` function for a long time, penalty may increase exponentially, not linearly as minter predicts.
Thus, minter will pay much more penalty than expected.

## Code Snippet
https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/MinterGateway.sol#L715
https://github.com/sherlock-audit/2023-10-mzero/blob/main/protocol/src/MinterGateway.sol#L759

## Tool used

Manual Review

## Recommendation
We recommend to modify the minter's penalty to always increase exponentially.
Then, both in the case that the long-term penalty is applied at one time and in the case that penalty is split-applied at several times, the penalty rate will coincide with each other.
